buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    jcenter()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def isTravis = "true".equals(System.getenv("TRAVIS"))

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        minSdkVersion 8
    }
    dexOptions {
        preDexLibraries = !isTravis
    }
}

dependencies {
    compile 'com.google.android.gms:play-services:6.5.87'
}

group = 'com.androidmapsextensions'
version = '2.1.1-SNAPSHOT'

if (!isTravis) {

    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    uploadArchives.repositories.mavenDeployer {

        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: sonatypeUserName, password: sonatypePassword)
        }
        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: sonatypeUserName, password: sonatypePassword)
        }

        pom.project {
            name 'Android Maps Extensions'
            description 'Library extending capabilities of Google Maps Android API v2'
            inceptionYear '2013'
            url 'http://AndroidMapsExtensions.com'
            packaging 'aar'

            developers {
                developer {
                    name 'Maciej GÃ³rski'
                    id 'mg6maciej'
                    email 'maciek.gorski@gmail.com'
                    url 'http://mg6.pl'
                }
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                url 'https://github.com/mg6maciej/android-maps-extensions'
                connection 'scm:git:https://github.com/mg6maciej/android-maps-extensions.git'
                developerConnection 'scm:git:https://github.com/mg6maciej/android-maps-extensions.git'
            }
        }

        pom.whenConfigured {
            pom -> pom.dependencies = []
        }
    }
}

android.libraryVariants.all { variant ->
    variant.outputs.each { output ->
        output.processManifest.doFirst {
            File manifestFile = file("${buildDir}/intermediates/exploded-aar/com.google.android.gms/play-services/6.5.87/AndroidManifest.xml")
            if (manifestFile.exists()) {
                String content = manifestFile.getText('UTF-8')
                content = content.replaceAll(/minSdkVersion="9"/, 'minSdkVersion="8"')
                manifestFile.write(content, 'UTF-8')
            }
        }
    }
}
